plugins {
    id 'java'
    id 'jvm-test-suite'
}

group = 'dev.rifaii'
version = '0.1'

repositories {
    mavenCentral()
}

dependencies {
    //Discord API wrapper
    implementation 'net.dv8tion:JDA:5.0.0-beta.13'

    //Jackson
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.2'

    //DB
    implementation 'redis.clients:jedis:4.4.3'

    //Validation
    implementation 'org.hibernate.validator:hibernate-validator:8.0.1.Final'
    implementation 'org.glassfish.expressly:expressly:5.0.0'

    //Logging
    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'ch.qos.logback:logback-core:1.4.11'
    implementation 'ch.qos.logback:logback-classic:1.4.11'

    //Lombok
    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'

    testCompileOnly 'org.projectlombok:lombok:1.18.28'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.28'

    //Testing
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mockito:mockito-core:5.5.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.5.0'
    testImplementation 'org.testcontainers:testcontainers:1.19.0'
    testImplementation 'org.testcontainers:junit-jupiter:1.19.0'
    testImplementation "com.redis.testcontainers:testcontainers-redis-junit:1.6.4"

}


testing {
    suites {
        test {
            useJUnitJupiter()
        }

        integtest(JvmTestSuite) {

            testType = TestSuiteType.INTEGRATION_TEST

            dependencies {
                implementation project()
            }

            configurations {
                integtestImplementation.extendsFrom testImplementation
                integtestRuntime.extendsFrom testRuntime
                integtestRuntimeOnly.extendsFrom testRuntimeOnly
            }
        }
    }
}